plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'com.optitem'
version = '1.0.0'

def paperVersion = '1.20.4-R0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

repositories {
    mavenCentral()
    maven { url = uri('https://repo.papermc.io/repository/maven-public/') }
    maven { url = uri('https://repo.codemc.io/repository/maven-public/') }
    maven { url = uri('https://jitpack.io') }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
    implementation 'de.tr7zw:item-nbt-api:2.13.1'
    compileOnly group: 'com.github.dmulloy2', name: 'ProtocolLib', version: '5.3.0'
}

tasks.processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(
            version: project.version
        )
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'de.tr7zw.changeme.nbtapi', 'com.optitem.libs.nbtapi'
}

jar {
    enabled = false
}

artifacts {
    archives shadowJar
}
